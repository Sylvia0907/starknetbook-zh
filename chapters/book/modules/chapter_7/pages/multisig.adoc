[id="multisig"]

= 多签账户

====
贡献：这个子章节缺少一个声明、部署和与合约交互的示例。我们很期待您的贡献！请提交一个 PR。
====

多重签名（multisig）技术是现代区块链领域的重要组成部分。它通过要求多个签名来确认交易，从而增强了安全性，减少了欺诈交易的风险，并增加了对资产管理的控制。

在 Starknet 中，多重签名账户的概念被抽象到协议层面，使开发人员能够实现体现该概念的自定义账户合约。在本章中，我们将深入探讨多重签名账户的工作原理，并看看如何使用合约账户在 Starknet 中创建它。

== 什么是多签账户？

多重签名账户是需要多个签名才能授权交易的账户。这显著增强了安全性，需要多个实体同意资金交易或执行关键操作。

多重签名账户的关键组成部分包括：

* 组成账户的公钥
* 所需的签名门槛数

由多重签名账户签名的交易必须由指定为该账户的不同密钥分别签名。如果签名数少于所需的门槛数，则产生的交易将不会执行成功。

在 Starknet 中，账户是在协议层面提供的抽象。因此，要创建多重签名账户，需要将逻辑编写到一个账户合约中并进行部署。

下面的合约是一个多重签名账户合约的示例。当部署时，它可以使用账户抽象的概念创建本地多重签名账户。请注意，这是一个简化示例，缺少在生产级多重签名合约中发现的全面检查和验证。

== 多签账户合约

[,Rust]
----
#[account_contract]
mod MultisigAccount {
    use ecdsa::check_ecdsa_signature;
    use starknet::ContractAddress;
    use zeroable::Zeroable;
    use array::ArrayTrait; 
    use starknet::get_caller_address;
    use box::BoxTrait;
    use array::SpanTrait;

    struct Storage {
        index_to_owner: LegacyMap::<u32, felt252>,
        owner_to_index: LegacyMap::<felt252, u32>,
        num_owners: usize,
        threshold: usize,
        curr_tx_index: felt252,
        //Mapping between tx_index and num of confirmations
        tx_confirms: LegacyMap<felt252, usize>,
        //Mapping between tx_index and it's execution state
        tx_is_executed: LegacyMap<felt252, bool>,
        //Mapping between a transaction index and its hash
        transactions: LegacyMap<felt252, felt252>,
        has_confirmed: LegacyMap::<(ContractAddress, felt252), bool>,
    }

    #[constructor]
    fn constructor(public_keys: Array::<felt252>, _threshold: usize) {
        assert(public_keys.len() <= 3_usize, 'public_keys.len <= 3');
        num_owners::write(public_keys.len());
        threshold::write(_threshold);
        _set_owners(public_keys.len(), public_keys);
    }

    //GETTERS
    //Get number of confirmations for a given transaction index
    #[view]
    fn get_confirmations(tx_index : felt252) -> usize {
        tx_confirms::read(tx_index)
    }

    //Get the number of owners of this account
    #[view]
    fn get_num_owners() -> usize {
        num_owners::read()
    }


    //Get the public key of the owners 
    //TODO - Recursively add the owners into an array and return, maybe wait for loops to be enabled


    //EXTERNAL FUNCTIONS

    #[external]
    fn submit_tx(public_key: felt252) {

        //Need to check if caller is one of the owners.
        let tx_info = starknet::get_tx_info().unbox();
        let signature: Span<felt252> = tx_info.signature;
        let caller = get_caller_address();
        assert(signature.len() == 2_u32, 'INVALID_SIGNATURE_LENGTH');

        //Updating the transaction index
        let tx_index = curr_tx_index::read();

        //`true` if a signature is valid and `false` otherwise.
        assert(
            check_ecdsa_signature(
                message_hash: tx_info.transaction_hash,
                public_key: public_key,
                signature_r: *signature.at(0_u32),
                signature_s: *signature.at(1_u32),
            ),
            'INVALID_SIGNATURE',
        );

        transactions::write(tx_index, tx_info.transaction_hash);
        curr_tx_index::write(tx_index + 1);

    }

    #[external]
    fn confirm_tx(tx_index: felt252, public_key: felt252) {

        let transaction_hash = transactions::read(tx_index);
        //TBD: Assert that tx_hash is not null

        let num_confirmations = tx_confirms::read(tx_index);
        let executed = tx_is_executed::read(tx_index);

        assert(executed == false, 'TX_ALREADY_EXECUTED');

        let caller = get_caller_address();
        let tx_info = starknet::get_tx_info().unbox();
        let signature: Span<felt252> = tx_info.signature;

         assert(
            check_ecdsa_signature(
                message_hash: tx_info.transaction_hash,
                public_key: public_key,
                signature_r: *signature.at(0_u32),
                signature_s: *signature.at(1_u32),
            ),
            'INVALID_SIGNATURE',
        );

        let confirmed = has_confirmed::read((caller, tx_index));

        assert (confirmed == false, 'CALLER_ALREADY_CONFIRMED');
        tx_confirms::write(tx_index, num_confirmations+1_usize);
        has_confirmed::write((caller, tx_index), true);


    }

    //An example function to validate that there are at least two signatures
    fn validate_transaction(public_key: felt252) -> felt252 {
        let tx_info = starknet::get_tx_info().unbox();
        let signature: Span<felt252> = tx_info.signature;
        let caller = get_caller_address();
        assert(signature.len() == 2_u32, 'INVALID_SIGNATURE_LENGTH');

        //`true` if a signature is valid and `false` otherwise.
        assert(
            check_ecdsa_signature(
                message_hash: tx_info.transaction_hash,
                public_key: public_key,
                signature_r: *signature.at(0_u32),
                signature_s: *signature.at(1_u32),
            ),
            'INVALID_SIGNATURE',
        );
        
        starknet::VALIDATED
    }

    //INTERNAL FUNCTION 
    //Function to add the public keys of the multi sig in permanent storage
    fn _set_owners(owners_len: usize, public_keys: Array::<felt252>) {
        if owners_len == 0_usize {
        }

        index_to_owner::write(owners_len, *public_keys.at(owners_len - 1_usize));
        owner_to_index::write(*public_keys.at(owners_len - 1_usize), owners_len);
        _set_owners(owners_len - 1_u32, public_keys);
    }


    #[external]
    fn __validate_deploy__(
        class_hash: felt252, contract_address_salt: felt252, public_key_: felt252
    ) -> felt252 {
        validate_transaction(public_key_)
    }

    #[external]
    fn __validate_declare__(class_hash: felt252, public_key_: felt252) -> felt252 {
        validate_transaction(public_key_)
    }

    #[external]
    fn __validate__(
        contract_address: ContractAddress, entry_point_selector: felt252, calldata: Array::<felt252>, public_key_: felt252
    ) -> felt252 {
        validate_transaction(public_key_)
    }

    #[external]
    #[raw_output]
    fn __execute__(
        contract_address: ContractAddress, entry_point_selector: felt252, calldata: Array::<felt252>, 
        tx_index: felt252
    ) -> Span::<felt252> {
        // Validate caller.
        assert(starknet::get_caller_address().is_zero(), 'INVALID_CALLER');

        // Check the tx version here, since version 0 transaction skip the __validate__ function.
        let tx_info = starknet::get_tx_info().unbox();
        assert(tx_info.version != 0, 'INVALID_TX_VERSION');

        //Multisig check here
        let num_confirmations = tx_confirms::read(tx_index);
        let owners_len = num_owners::read();
        //Subtracting one for the submitter
        let required_confirmations = threshold::read() - 1_usize;
        assert(num_confirmations >= required_confirmations, 'MINIMUM_50%_CONFIRMATIONS');

        tx_is_executed::write(tx_index, true);

        starknet::call_contract_syscall(
            contract_address, entry_point_selector, calldata.span()
        ).unwrap_syscall()
    }
}
----

== 多签交易的流程

多重签名交易的流程包括以下步骤：

1. 提交交易：任何一个账户所有者都可以从该账户提交交易。
2. 确认交易：没有提交交易的账户所有者可以确认该交易。

如果确认的数量（包括提交者的签名）大于或等于所需签名门槛数，则交易将成功执行，否则交易将失败。这种确认机制确保没有任何单个方可以单方面执行关键交易，从而增强了账户的安全性。

== 探索多重签名函数

让我们更仔细地看一下提供的合约中与多重签名功能相关的各种函数。

=== _set_owners 函数

这是一个内部函数，旨在将账户所有者的公钥添加到永久存储中。理想情况下，多重签名账户结构应允许根据账户所有者的协议添加和删除所有者。然而，每次更改都应该是一个需要达到签名门槛数的交易。

[,Rust]
----
//INTERNAL FUNCTION 
//Function to add the public keys of the multi sig in permanent storage
fn _set_owners(owners_len: usize, public_keys: Array::<felt252>) {
    if owners_len == 0_usize {
    }

    index_to_owner::write(owners_len, *public_keys.at(owners_len - 1_usize));
    owner_to_index::write(*public_keys.at(owners_len - 1_usize), owners_len);
    _set_owners(owners_len - 1_u32, public_keys);
}
----

=== submit_tx 函数

这是一个外部函数，允许账户所有者提交交易。提交后，该函数会检查交易的有效性，确保调用者是账户所有者之一，并将交易添加到交易映射中。它还会增加当前交易索引。 

[,Rust]
----
#[external]
fn submit_tx(public_key: felt252) {

    //Need to check if caller is one of the owners.
    let tx_info = starknet::get_tx_info().unbox();
    let signature: Span<felt252> = tx_info.signature;
    let caller = get_caller_address();
    assert(signature.len() == 2_u32, 'INVALID_SIGNATURE_LENGTH');

    //Updating the transaction index
    let tx_index = curr_tx_index::read();

    //`true` if a signature is valid and `false` otherwise.
    assert(
        check_ecdsa_signature(
            message_hash: tx_info.transaction_hash,
            public_key: public_key,
            signature_r: *signature.at(0_u32),
            signature_s: *signature.at(1_u32),
        ),
        'INVALID_SIGNATURE',
    );

    transactions::write(tx_index, tx_info.transaction_hash);
    curr_tx_index::write(tx_index + 1);

}
----

=== confirm_tx 函数

类似地， *_confirm_tx_* 函数提供了一种记录每个交易确认的方法。未提交交易的账户所有者可以确认该交易，从而增加其确认计数。

[Source, Rust]
----
    #[external]
    fn confirm_tx(tx_index: felt252, public_key: felt252) {

        let transaction_hash = transactions::read(tx_index);
        //TBD: Assert that tx_hash is not null

        let num_confirmations = tx_confirms::read(tx_index);
        let executed = tx_is_executed::read(tx_index);

        assert(executed == false, 'TX_ALREADY_EXECUTED');

        let caller = get_caller_address();
        let tx_info = starknet::get_tx_info().unbox();
        let signature: Span<felt252> = tx_info.signature;

         assert(
            check_ecdsa_signature(
                message_hash: tx_info.transaction_hash,
                public_key: public_key,
                signature_r: *signature.at(0_u32),
                signature_s: *signature.at(1_u32),
            ),
            'INVALID_SIGNATURE',
        );

        let confirmed = has_confirmed::read((caller, tx_index));

        assert (confirmed == false, 'CALLER_ALREADY_CONFIRMED');
        tx_confirms::write(tx_index, num_confirmations+1_usize);
        has_confirmed::write((caller, tx_index), true);
    }
----

=== __execute__ 函数

__execute__ 函数作为交易过程的最后一步。它检查交易的有效性、是否已经执行过以及是否已经达到了签名门槛数。如果所有检查都通过，则执行交易。

[,Rust]
----
#[external]
    #[raw_output]
    fn __execute__(
        contract_address: ContractAddress, entry_point_selector: felt252, calldata: Array::<felt252>, 
        tx_index: felt252
    ) -> Span::<felt252> {
        // Validate caller.
        assert(starknet::get_caller_address().is_zero(), 'INVALID_CALLER');

        // Check the tx version here, since version 0 transaction skip the __validate__ function.
        let tx_info = starknet::get_tx_info().unbox();
        assert(tx_info.version != 0, 'INVALID_TX_VERSION');

        //Multisig check here
        let num_confirmations = tx_confirms::read(tx_index);
        let owners_len = num_owners::read();
        //Subtracting one for the submitter
        let required_confirmations = threshold::read() - 1_usize;
        assert(num_confirmations >= required_confirmations, 'MINIMUM_50%_CONFIRMATIONS');

        tx_is_executed::write(tx_index, true);

        starknet::call_contract_syscall(
            contract_address, entry_point_selector, calldata.span()
        ).unwrap_syscall()
    }
----

== 最后的总结

本章介绍了在 Starknet 中使用多重签名账户的概念，并演示了如何使用账户合约实现。但是，需要注意的是，这只是一个简化的示例，一个生产级别的多重签名合约应该包含额外的检查和验证以确保其鲁棒性和安全性。

[NOTE]
====
这本书是社区贡献出的成果，为社区而创建。

* 如果您学到了一些东西，或者没有收获，也请花点时间通过这个 https://a.sprig.com/WTRtdlh2VUlja09lfnNpZDo4MTQyYTlmMy03NzdkLTQ0NDEtOTBiZC01ZjAyNDU0ZDgxMzU=[三个问题的调查]提供反馈。
* 如果您发现任何错误或有其他建议，请毫不犹豫地在我们的 https://github.com/starknet-edu/starknetbook/issues[GitHub 存储库上开一个 issue]。
====

== **贡献力量**

> 释放你的热情，让《Starknet 之书》更加完美
> 
> 
> *《Starknet 之书》依然在不断完善中，而你的热情、专业知识和独到见解可以将它塑造成一部真正卓越的作品。不要害怕挑战现状或是颠覆这本书！齐心协力，我们一起创造这份宝贵的资源，造福无数人。*
> 
> *为公共事业贡献力量。如果你发现本书有改进的空间，那就抓住机会吧！查看我们的 https://github.com/starknet-edu/starknetbook/blob/main/CONTRIBUTING.adoc[指南] 并加入活力满满的社区。一起无畏共建 Starknet！*
> 

— Starknet 社区