= 签名的过程和一个标准账户合约

在本章中，我们在对 Starknet 账户合约探索的基础上，介绍了一个更复杂的账户合约。这个标准账户合约包括一些额外的功能，如签名验证，提供了一个更强大的 Cairo 语言账户合约示例。您可以在书籍存储库中找到此合约的完整代码（todo：添加链接）。您可以使用 Scarb 与合约进行交互和编译（有关更多信息，请查看本书第2章中的 Scarb 子章节）。

[,Rust]
----
// Import necessary modules and traits
use serde::Serde;
use starknet::ContractAddress;
use array::ArrayTrait;
use array::SpanTrait;
use option::OptionTrait;

// Define the Account contract
#[account_contract]
mod Account {
    use array::ArrayTrait;
    use array::SpanTrait;
    use box::BoxTrait;
    use ecdsa::check_ecdsa_signature;
    use option::OptionTrait;
    use super::Call;
    use starknet::ContractAddress;
    use zeroable::Zeroable;
    use serde::ArraySerde;

    // Define the contract's storage variables
    struct Storage {
        public_key: felt252
    }

    // Constructor function for initializing the contract
    #[constructor]
    fn constructor(public_key_: felt252) {
        public_key::write(public_key_);
    }
    
    // Internal function to validate the transaction signature
    fn validate_transaction() -> felt252 {
        let tx_info = starknet::get_tx_info().unbox(); // Unbox transaction info
        let signature = tx_info.signature; // Extract signature
        assert(signature.len() == 2_u32, 'INVALID_SIGNATURE_LENGTH'); // Check signature length

        // Verify ECDSA signature
        assert(
            check_ecdsa_signature(
                message_hash: tx_info.transaction_hash, 
                public_key: public_key::read(),
                signature_r: *signature[0_u32],
                signature_s: *signature[1_u32],
            ),
            'INVALID_SIGNATURE',
        );

        starknet::VALIDATED // Return validation status
    }

    // Validate contract deployment
    #[external]
    fn __validate_deploy__(
        class_hash: felt252, contract_address_salt: felt252, public_key_: felt252
    ) -> felt252 {
        validate_transaction()
    }

    // Validate contract declaration
    #[external]
    fn __validate_declare__(class_hash: felt252) -> felt252 {
        validate_transaction()
    }

    // Validate contract execution
    #[external]
    fn __validate__(
        contract_address: ContractAddress, entry_point_selector: felt252, calldata: Array<felt252>
    ) -> felt252 {
        validate_transaction()
    }

    // Execute a contract call
    #[external]
    #[raw_output]
    fn __execute__(mut calls: Array<Call>) -> Span<felt252> {
        // Validate caller
        assert(starknet::get_caller_address().is_zero(), 'INVALID_CALLER');

        let tx_info = starknet::get_tx_info().unbox(); // Unbox transaction info
        assert(tx_info.version != 0, 'INVALID_TX_VERSION');

        assert(calls.len() == 1_u32, 'MULTI_CALL_NOT_SUPPORTED'); // Only single calls are supported
        let Call{to, selector, calldata } = calls.pop_front().unwrap();

        // Call the target contract
        starknet::call_contract_syscall(
            address: to, entry_point_selector: selector, calldata: calldata.span()
        ).unwrap_syscall()
    }
}

// Define the Call struct
#[derive(Drop, Serde)]
struct Call {
    to: ContractAddress,
    selector: felt252,
    calldata: Array<felt252>
}
----

== 理解 ECDSA 签名

椭圆曲线数字签名算法（ECDSA）是一种广泛应用于各种区块链的加密协议，用于确保数据的完整性和验证发送方的真实性。作为数字签名算法（DSA）的一种变体，ECDSA利用椭圆曲线加密，提供了比传统 DSA 更短的密钥更高的安全性。

ECDSA 签名包括两个组件，通常称为 “r” 和 “s”。这两个值是使用签名者的私钥和被签名的消息（或交易）的哈希值生成的，共同形成给定输入的签名。

=== 解密 signature_r 和 signature_s

在标准账户合约的上下文中，“signature_r” 和 “signature_s” 表示ECDSA签名的两个组成部分。这些在 “check_ecdsa_signature” 函数中用于验证交易的合法性。

* `signature_r (r)`：在签名过程中生成的随机数，每个签名都是唯一的。在不同的消息中重复使用 'r' 可能会导致私钥暴露。
* `signature_s (s)`：这是使用 'r'、私钥和消息的哈希值来计算的。与 'r' 一样，'s' 对于每个签名也是唯一的。

函数 “check_ecdsa_signature” 获取这两个值、签名者的公钥和消息的哈希值，以验证签名。有效的签名表示消息确实是由私钥所有者签名并保持不变的。

[source,rust]
----
        assert(
            check_ecdsa_signature(
                message_hash: tx_info.transaction_hash, 
                public_key: public_key::read(),
                signature_r: *signature[0_u32],
                signature_s: *signature[1_u32],
            ),
            'INVALID_SIGNATURE',
        );
----

上面的代码片段使用 “check_ecdsa_signature” 函数来断言交易签名的合法性。如果签名无效，断言将失败并返回 “INVALID_SIGNATURE”。

== 合约结构

=== 存储（Storage）

在标准账户合约中，我们声明了一个单独的存储变量：“public_key”。这有助于交易签名验证。公钥以 “felt252”（一个252位的整数）的形式存储在构造函数中，并从存储中访问 “validate_transaction” 函数。

[source,rust]
----
struct Storage {
    public_key: felt252
}
----

=== 构造函数（Constructor）

构造函数用于初始化合约，将提供的公钥存储在合约的存储中。

[source,rust]
----
#[constructor]
fn constructor(public_key_: felt252) {
    public_key::write(public_key_);
}
----

=== validate_transaction 函数

这个内部函数用于验证交易签名。它从交易信息中检索签名，检查其长度并验证 ECDSA 签名。如果签名合法，则返回 starknet::VALIDATED，否则返回错误。这个函数由“validate_deploy”、“validate_declare” 和 “validate” 函数调用。

是否包含这个函数是可选的。如果不需要交易签名验证，可以省略它。然而，建议在您的账户合约中包含它，以确保交易的有效性并便于在所有三个验证函数中重复使用。

[source,rust]
----
fn validate_transaction() -> felt252 {
        let tx_info = starknet::get_tx_info().unbox(); // Unbox transaction info
        let signature = tx_info.signature; // Extract signature
        assert(signature.len() == 2_u32, 'INVALID_SIGNATURE_LENGTH'); // Check signature length

        // Verify ECDSA signature
        assert(
            check_ecdsa_signature(
                message_hash: tx_info.transaction_hash, 
                public_key: public_key::read(),
                signature_r: *signature[0_u32],
                signature_s: *signature[1_u32],
            ),
            'INVALID_SIGNATURE',
        );

        starknet::VALIDATED // Return validation status
    }
----

=== Call 结构体

Call 结构体概述了合约调用所需的参数。这些参数包括目标合约地址（to）、要调用的函数（selector）以及函数的参数（calldata）。Call结构体在 __execute__ 函数中被使用。

[source,rust]
----
#[derive(Drop, Serde)]
struct Call {
    to: ContractAddress,
    selector: felt252,
    calldata: Array<felt252>
}
----

=== execute 函数

此外部函数在成功验证后触发交易。它确保调用者的有效性，检查非零交易版本，并且仅支持单个调用。验证后，它将调用转发到目标合约。合约创建者可以在此功能内部将多个调用合并到不同合约或同一合约（多重调用）。该函数返回来自目标合约的输出。

[source,rust]
----
    #[external]
    #[raw_output]
    fn __execute__(mut calls: Array<Call>) -> Span<felt252> {
        // Validate caller
        assert(starknet::get_caller_address().is_zero(), 'INVALID_CALLER');

        let tx_info = starknet::get_tx_info().unbox(); // Unbox transaction info
        assert(tx_info.version != 0, 'INVALID_TX_VERSION');

        assert(calls.len() == 1_u32, 'MULTI_CALL_NOT_SUPPORTED'); // Only single calls are supported
        let Call{to, selector, calldata } = calls.pop_front().unwrap();

        // Call the target contract
        starknet::call_contract_syscall(
            address: to, entry_point_selector: selector, calldata: calldata.span()
        ).unwrap_syscall()
    }
----

== 标准账户合约的改进

标准账户合约的实现有一些限制：

* 它目前只支持单个调用。我们可以支持多个调用以提高合约的灵活性和实用性。
* ECDSA 签名算法虽然安全，但计算密集。未来的版本可以探索使用更高效的签名算法，如 Schnorr 或 BLS。或者使用量子抗性签名算法，如 STARKs。

尽管有这些限制，标准账户合约为在Starknet上创建和交互智能合约提供了坚实而安全的基础。


== 声明和部署“Hello World”账户合约

这次我们有一个构造函数，它以公钥作为参数。我们需要生成一个具有相应公钥的私钥。 

TODO: 添加如何生成私钥和公钥的部分。

* 导出所需的环境变量：

[source,bash]
----
export STARKNET_NETWORK=alpha-goerli
export STARKNET_WALLET=starkware.starknet.wallets.open_zeppelin.OpenZeppelinAccount
----

* 声明合约（由于合约已经声明，您可以跳过此步骤。如果您仍想声明它，请运行以下命令，但会收到错误提示）：

[source,bash]
----
starknet declare --contract target/release/starknetbook_chapter_7_Account.json --account vote_admin --max_fee 100000000000000000
----

类哈希为：0x05501f7806d3d11cab101e19001e409dd4760200c2da2fe03761750f66e4a5e1

* 部署合约：

TODO: 添加如何部署合约的部分。


[NOTE]
====
椭圆曲线数字签名算法（ECDSA）是保证区块链网络中数据完整性和发送者身份验证的流行选择，但它并不是唯一的选择。其他替代方案包括：

* EdDSA（Edwards曲线数字签名算法）：EdDSA 是另一种椭圆曲线加密形式，旨在比 ECDSA 更快速且更安全。EdDSA 使用扭曲 Edwards 曲线，具有强大的安全性能并允许更高效的计算。Monero 就是一个使用 EdDSA 的例子。

* Schnorr 签名：Schnorr 签名提供了与 ECDSA 类似的安全级别，但具有更短的签名长度。它们还具有线性属性，可以实现签名聚合和多重签名。这可以提高效率和隐私。比特币开发人员已经提议在 Taproot 升级中向比特币协议添加 Schnorr 签名。

* RSA（Rivest-Shamir-Adleman）：RSA 是一种较旧的加密算法，广泛用于安全数据传输。然而，RSA需要更大的密钥大小才能达到相同的安全级别，使其比椭圆曲线技术不太高效。RSA在现代区块链系统中不常用，但仍然在许多传统的安全通信协议中使用。

* BLS（Boneh-Lynn-Shacham）签名：与 Schnorr 一样，BLS 签名允许签名聚合，使其在需要大量签名的系统中非常有用。这种属性使 BLS 签名在分布式系统和区块链（如以太坊2.0）中的共识算法中特别有用。

* 后量子密码学：随着量子计算的出现，研究人员正在开发抗量子攻击的新的加密算法。一个例子是 Starknet 中使用的 STARKs。

这些替代方案在安全性、效率、复杂性和数学属性等方面各有优劣。
====

== 总结

在本章中，我们通过研究一个更复杂的“标准账户合约”来扩展我们对 Starknet 账户合约的理解。我们深入探讨了合约的各个组成部分，并学习了它们如何协同工作来验证和执行交易。

[NOTE]
====
这本书是社区贡献出的成果，为社区而创建。

* 如果您学到了一些东西，或者没有收获，也请花点时间通过这个 https://a.sprig.com/WTRtdlh2VUlja09lfnNpZDo4MTQyYTlmMy03NzdkLTQ0NDEtOTBiZC01ZjAyNDU0ZDgxMzU=[三个问题的调查]提供反馈。
* 如果您发现任何错误或有其他建议，请毫不犹豫地在我们的 https://github.com/starknet-edu/starknetbook/issues[GitHub 存储库上开一个 issue]。
====

== **贡献力量**

> 释放你的热情，让《Starknet 之书》更加完美
> 
> 
> *《Starknet 之书》依然在不断完善中，而你的热情、专业知识和独到见解可以将它塑造成一部真正卓越的作品。不要害怕挑战现状或是颠覆这本书！齐心协力，我们一起创造这份宝贵的资源，造福无数人。*
> 
> *为公共事业贡献力量。如果你发现本书有改进的空间，那就抓住机会吧！查看我们的 https://github.com/starknet-edu/starknetbook/blob/main/CONTRIBUTING.adoc[指南] 并加入活力满满的社区。一起无畏共建 Starknet！*
> 

— Starknet 社区